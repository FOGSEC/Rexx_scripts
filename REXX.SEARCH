/* REXX */
/* Script to search for a string inside Datasets
   Execute from TSO : EXEC 'REXX.SEARCH' '<HLQ> <STRING>'
   @uthor : Ayoul3

   TODO : Support any level qualifier
          Support reading line by line
          Support output format (DS,USS file, socket)
          Ask user which type of DS to searhc for

*/
arg HLQ STRING

/* debug */
HLQ = "SEARCH2"
STRING = "AYOUB"
/* fin debug*/

if STRING="" then do
  say "usage TSO : exec REXX.SEARCH '<HLQ> <STRING>' "
  say "usage USS : ./REXX.SEARCH <HLQ> <STRING> "
  say "note : if you are on TSO, do not forget the quotes"
  exit
 end


if HLQ="" then do
 HLQ='REXX'
end

say "Searching for " STRING " in datasets "HLQ".*"
say ""
HLQ=strip(strip(HLQ,"T","*"),"T",".")

call outtrap "listcat."
address tso "LISTCAT LEVEL("HLQ")"
call outtrap "off"

if RC <>0 then do
  say "No datasets found with the HLQ "HLQ
  exit
end

do i=1 to listcat.0
   dt_name = word(listcat.i,3)
   call listdsi "'"dt_name"'"


     if sysdsorg ="PO" then
     do
        call outtrap "mbrs."
        address tso "LISTDS" "'"dt_name"' members"
        call outtrap 'off'
        idx = 0
        do j=1 to mbrs.0
          if mbrs.j ="--MEMBERS--" then do
             idx=1
          end
          if idx =1 & mbrs.j \= "--MEMBERS--" then do
             tmp = dt_name||"("||strip(mbrs.j,'B')||")"
             call searchDS tmp,STRING
          end
        end

     end

     else if sysdsorg="VS" & word(listcat.i,1) <> "IN-CAT" then do
       call outtrap "entries."
       address tso "LISTCAT ENTRY('"dt_name"')"
       call outtrap "off"

       if word(entries.1,1) ="DATA" then do
            call searchVS dt_name,STRING
        end
       else if word(entries.1,1)="CLUSTER" then do
            DT_DATA = word(entries.3,3)
            parse var DT_DATA HLQ_D "." REST
             if HLQ_D <> HLQ then do
                call searchVS DT_DATA,STRING
             end
       end
     end

     else if sysdsorg ="PS" then do
     call searchDS dt_name,STRING
     end
end
exit

searchVS:
procedure
    DT_NAME = arg(1)
    STRING = translate(arg(2))
    call outtrap "lines."
    address tso "PRINT INDATASET('"DT_NAME"')"
    call outtrap "off"
    if RC <>0 then  do
       say "[#] Error reading VSAM file " DT_NAME
    end
    do k=1 to lines.0
      if (index(lines.k,STRING) > 0) then,
      do
        say DT_NAME k lines.k
      end
    end
    return

exit
searchDS:
procedure
    DT_NAME =arg(1)
    STRING = translate(arg(2))
    DT_NAME = strip(DT_NAME,'B')
    address tso "alloc file(input) dataset('"DT_NAME"') SHR"
    if RC \= 0 then
       do
          say '[#] Error allocating file' DT_NAME
       end

    address tso "execio * diskr input (stem input. finis)"
    address tso "free file(input)"

    do j=1 to input.0
       input.j =translate(input.j)
       if (index(input.j,STRING) > 0) then,
         do
            say DT_NAME j input.j
         end
    end
    return
